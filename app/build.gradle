plugins {
    id 'kotlin-android'

    id 'kotlin-kapt'
    id 'com.android.application'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.iteminnovation.android.stardroid"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1541
        versionName "1.10.1 - RC1"
        buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', '""'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    task prepareKotlinBuildScriptModel {

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if (project.file('no-checkin.properties').exists()) {
                Properties properties = new Properties()
                // Sky Map devs should create this file with the signing passwords
                properties.load(project.file('no-checkin.properties').newDataInputStream())
                storeFile file("stardroid-release-key.keystore")
                storePassword properties.getProperty('store-pwd')
                keyPassword properties.getProperty('key-pwd')
                keyAlias "stardroid"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            if (file('no-checkin.properties').exists()) {
                signingConfig signingConfigs.release
                Properties properties = new Properties()
                // Sky Map devs should create this file with the Google Analytics key
                properties.load(project.file('no-checkin.properties').newDataInputStream())
                def analyticsKey = properties.getProperty('analytics-key')
                buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', analyticsKey
            }
        }
    }

    lintOptions {
        // Lint complains because of missing translations.
        // (Missing translations now removed)
        abortOnError false
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d", "-t"
    }
    
    testOptions {
        animationsDisabled = true
    }

    flavorDimensions "default"
    productFlavors {
        // includes gms Google services
        gms {
            resValue "string", "analytics_enabled", "true"
        }

        // uses only open source software
        fdroid {
            resValue "string", "analytics_enabled", "false"
        }
    }
}

dependencies {
    implementation project(path: ':datamodel')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

// Android support
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.core:core-ktx:1.9.0"

// Third-party
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    def dagger_version = '2.45'
     implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"


    // For annotations not in Android but needed by Dagger
    implementation 'javax.annotation:jsr250-api:1.0'
    // Lite version for Android - it's lighter (obviously) but also doesn't use reflection
    // which can cause no end of problems with minifiers like proguard.
    implementation 'com.google.protobuf:protobuf-javalite:3.13.0'

// Flavor
    gmsImplementation 'com.google.android.gms:play-services-analytics:18.0.2'
    gmsImplementation 'com.google.android.gms:play-services-location:21.0.1'
    gmsImplementation 'com.google.firebase:firebase-analytics:21.2.0'

// Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'org.easymock:easymock:5.1.0'
    testImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.easymock:easymock:5.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // Optional -- UI testing with UI Automator
    // androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
